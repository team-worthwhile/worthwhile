grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// A program consists of a sequence of statements and/or function declarations.
Program:
	{Program}
	NL* // Newline at the beginning of the file is allowed. 
	((statements+=Statement | functions+=FunctionDeclaration) NL+)* // All statements need a newline after them. FIXME: End of file?
;

// Override the whitespace rules: Disallow line breaks as whitespace since we want to use them to separate statements 
terminal WS : (' '|'\t')+;

// A newline consists of at least one '\n'.
terminal NL : ('\r' | '\n' | WS)* '\n' WS*;

// A single line comment includes everything from // to the next newline, but not the newline itself.
terminal SL_COMMENT : 
	'//' !('\n'|'\r')*
;

terminal NUMBER returns ecore::EBigInteger:
 	('0'..'9')+
 ;
 
terminal INT returns ecore::EInt:
 	"$$$don't use this anymore$$$"
 ;


// A block consists of one or multiple statements enclosed in curly brackets.
Block:
	{Block} '{' NL* (statements+=Statement NL+)* '}'
;

// A statement is everything that can be executed in the program.
Statement:
	(VariableDeclaration | Assignment | Block | Annotation | FunctionCall | IfStatement | WhileStatement | ReturnStatement)
;

// A return statement returns a value from a function.
ReturnStatement:
	'return' returnExpression=Expression
;

// A while statement specifies a while loop.
WhileStatement:
	'while' condition=Expression (invariants+=Invariant)* body=Block
;

// 
Invariant:
	'_invariant' Expression=(Expression | QuantifiedExpression) NL+ 
;

// An if statement specifies a conditional Expression.
IfStatement:
	'if' condition=Expression then=Block ('else' else=Block)?
;

// A function declaration declares a function name, parameters, return type, and function body.
FunctionDeclaration returns Function:
	'function' returnType=Type name=ID '(' (params+=Parameter (',' params+=Parameter)* )? ')' NL* (requires+=Require | ensures+=Ensure)* body=Block 
;

Require:
	'_requires' Expression=(Expression | QuantifiedExpression) NL+ 
;

Ensure:
	'_ensures' Expression=(Expression | QuantifiedExpression) NL+ 
;
 
// A parameter definition describes a single parameter (type and name) accepted by a function.
Parameter returns Variable:
	{Parameter} type=Type name=ID
;

// A type can either be a primitive type or an array of a primitive type
Type:
	PrimitiveType | ArrayType
;

// A primitive type represents a variable type that can hold one value.
PrimitiveType:
	{IntType} 'Integer' | {BoolType} 'Boolean'	
;

// An array type represents an array of a primitive type.
ArrayType:
	{ArrayType} baseType=PrimitiveType '[' size=Expression ']'
;

// A function call simply calls a function without returning a value.
FunctionCall:
	{FunctionRef} function=[Function] '(' (actuals+=FunctionCallActualParameter)? (',' actuals+=FunctionCallActualParameter)* ')'
;

FunctionCallActualParameter:
	{FunctionCallActualParameter} value=Expression
;
 
// An annotation is used to specify assertions and assumptions.
Annotation:
	 ({Assertion} '_assert' | {Assumption} '_assume' | {Axiom} '_axiom') Expression=(Expression | QuantifiedExpression)
;

// A quantified Expression is an Expression optionally preceded by some quantifier definitions.
QuantifiedExpression:
	(({ForAllExpression} ('forall' | '∀') | {ExistsExpression} ('exists' | '∃')) var=VariableDeclarationWithoutInitialValue (',' condition=Expression)?) (Expression=QuantifiedExpression | ':' Expression=Expression)
;

// A variable declaration declares a new variable and assigns it a value.
VariableDeclaration returns Variable:
	{VariableDeclaration} type=Type name=ID ':=' initialValue=Expression
;

// This variable declaration declares a new variable, but does not assign it a value
VariableDeclarationWithoutInitialValue returns Variable:
	type=Type name=ID
;

// An assignment assigns a value to a variable.
Assignment:
	variable=VariableRef ':=' value=Expression
;

// An Expression is based on operator precedence. Each "level" of Expressions means one level of operator precedence,
// where the last operation specified has the highest priority. In the same level, operators are evaluated from left to right.
Expression:
	BooleanOr
;

// The boolean or has the lowest priority.
BooleanOr returns Expression:
	BooleanAnd ({Or.left=current} ('∨' | '||') right=BooleanAnd)?
;

// The boolean and has second lowest priority.
BooleanAnd returns Expression:
	EqualsComparison ({And.left=current} ('∧' | '&&') right=EqualsComparison)?
;

// The equals comparison is next in priority.
EqualsComparison returns Expression:
	Comparison ({Equals.left=current} '=' right=Comparison)?
;

// A comparison of two integer values is next in priority.
Comparison returns Expression:
	Addition (({Less.left=current} '<' | {LessOrEqual.left=current} ('<=' | '≤') | {GreaterOrEqual.left=current} ('>=' | '≥') | {Greater.left=current} '>') right=Addition)?
;

// Next in priority are addition and subtraction
Addition returns Expression:
	Multiplication (({Plus.left=current} '+' | {Minus.left=current} '-') right=Multiplication)*
;

// Next in priority are multiplication, division, and modulo.
Multiplication returns Expression:
	UnaryOperation (({Multi.left=current} ('*' | '·') | {Div.left=current} ('/' | '÷') | {Modulo.left=current} '%') right=UnaryOperation)*
;

// The unary operations (unary plus, unary minus, Boolean not) have second highest priority.
UnaryOperation returns Expression:
	 ArrayAccess | (({UnaryMinusOperator} '-' | {UnaryPlusOperator} '+' | {UnaryNotOperator} ('¬'|'!') ) Expression=ArrayAccess)
;

// The access of array items via an index has highest priority.
ArrayAccess returns Expression:
	Atomic ({ArrayAccess.Expression=current} "[" index=Expression "]")?
;

// An atomic value in an Expression is either
//  - a function call or
//  - a number or
//  - a Boolean literal or
//  - a reference to a variable
//  - a whole new Expression in parentheses.
Atomic returns Expression:
	FunctionCall | {NumberLiteral} value=NUMBER | BooleanLiteral | VariableRef | '(' expr=Expression ')'
;

// A Boolean literal is either "true" or "false"
BooleanLiteral:
	{TrueLiteral} 'true' | {FalseLiteral} 'false'
;

// A variable reference is used to denote the current content of a variable. 
VariableRef:
	{VariableRef} variable=[Variable] // TODO ('[' index=Expression ']')?
;